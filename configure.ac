

# version of this package
m4_define(gaim_meanwhile_version,	1.2.1cvs)

# release of this package
m4_define(gaim_meanwhile_release,	0)

# lower and upper-bound versions of Meanwhile
m4_define(meanwhile_version_min,	0.4.1)
m4_define(meanwhile_version_max,	0.5.0)

# lower and upper-bound versions of Gaim
m4_define(gaim_version_min,		1.2.0)
m4_define(gaim_version_max,		2.0.0)



AC_INIT
AM_INIT_AUTOMAKE(gaim-meanwhile, gaim_meanwhile_version)

AC_PREREQ([2.50])

AC_PROG_CC

AC_DISABLE_STATIC
AC_PROG_LIBTOOL
LIBTOOL="$LIBTOOL --silent"

AC_PROG_INSTALL

AC_HEADER_STDC
AC_HEADER_SYS_WAIT

AC_C_CONST
AC_STRUCT_TM



# substitutions used in gaim-meanwhile.spec.in
RELEASE=gaim_meanwhile_release
AC_SUBST(RELEASE)

GAIM_MIN=gaim_version_min
GAIM_MAX=gaim_version_max
AC_SUBST(GAIM_MIN)
AC_SUBST(GAIM_MAX)

MEANWHILE_MIN=meanwhile_version_min
MEANWHILE_MAX=meanwhile_version_max
AC_SUBST(MEANWHILE_MIN)
AC_SUBST(MEANWHILE_MAX)



# tell pkgconfig to look in the same prefix we're installing this to,
# as that's likely where gaim will be found if it's not in the default
# pkgconfig path
PREFIX=$ac_default_prefix
if test "$prefix" != "NONE"; then
   PREFIX=$prefix
fi

PKG_CONFIG_PATH="$PKG_CONFIG_PATH":"$PREFIX/lib/pkgconfig"
export PKG_CONFIG_PATH


withval="Talk to me"
AC_ARG_WITH(active_msg,
	[ --with-active-msg=["Talk to me"]
                                     define the default active message])
CONF_ACTIVE_MSG=$withval
AC_SUBST(CONF_ACTIVE_MSG)


withval="Not here right now"
AC_ARG_WITH(away_msg,
	[ --with-away-msg=["Not here right now"]
	                             define the default away message])
CONF_AWAY_MSG=$withval
AC_SUBST(CONF_AWAY_MSG)


withval="Please do not disturb me"
AC_ARG_WITH(busy_msg,
	[ --with-busy-msg=["Please do not disturb me"]
				     define the default busy message])
CONF_BUSY_MSG=$withval
AC_SUBST(CONF_BUSY_MSG)


withval=""
AC_ARG_WITH(host,
	[ --with-host=[""]           define the default community host])
CONF_DEFAULT_HOST=$withval
AC_SUBST(CONF_DEFAULT_HOST)


withval=1533
AC_ARG_WITH(port,
	[ --with-port=[1533]         define the default community port])
CONF_DEFAULT_PORT=$withval
AC_SUBST(CONF_DEFAULT_PORT)


withval=mwLogin_MEANWHILE
AC_ARG_WITH(client,
	[ --with-client=[mwLogin_MEANWHILE]
                                     define the client identifier])
CONF_CLIENT_TYPE=$withval
AC_SUBST(CONF_CLIENT_TYPE)


# debug mode
AC_ARG_ENABLE(debug,
	[  --enable-debug	  compile with debugging support],,
	enable_debug=no)

if test "$enable_debug" = yes; then
   AC_DEFINE(DEBUG, 1, [Define if debugging is enabled.])
fi



# Check for Gaim
PKG_CHECK_MODULES(GAIM,
[gaim >= gaim_version_min gaim < gaim_version_max],
[
	AC_DEFINE(HAVE_GAIM, 1, [Define if we've found gaim.])
])

AC_SUBST(GAIM_CFLAGS)
AC_SUBST(GAIM_LIBS)



# Check for Meanwhile
PKG_CHECK_MODULES(MEANWHILE,
[meanwhile >= meanwhile_version_min meanwhile < meanwhile_version_max],
[
	AC_DEFINE(HAVE_MEANWHILE, 1, [Define if we've found meanwhile.])
])

AC_SUBST(MEANWHILE_CFLAGS)
AC_SUBST(MEANWHILE_LIBS)



# Check for GLib
PKG_CHECK_MODULES(GLIB,
[glib-2.0 >= 2.0.0],
[
	AC_DEFINE(HAVE_GLIB, 1, [Define if we've found glib.])
])


AC_SUBST(GLIB_CFLAGS)
AC_SUBST(GLIB_LIBS)


AC_CONFIG_FILES(
	[src/config.h]
	[Makefile dist/Makefile pixmaps/Makefile src/Makefile]
	[gaim-meanwhile.spec]
)

AC_OUTPUT()


echo
echo Default active message... : \"$CONF_ACTIVE_MSG\"
echo Default away message..... : \"$CONF_AWAY_MSG\"
echo Default busy message..... : \"$CONF_BUSY_MSG\"
echo Default community host... : \"$CONF_DEFAULT_HOST\"
echo Default community port... : $CONF_DEFAULT_PORT
echo Client type identifier... : $CONF_CLIENT_TYPE
echo
echo configure complete, now run \`make\`
echo

# The End.
